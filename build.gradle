apply plugin: 'fatjar'

version       = "0.0.1"

buildscript {
  dependencies {
    classpath 'eu.appsatori:gradle-fatjar-plugin:0.1.2' // adds fatJar task
  }
  repositories {
    mavenCentral()
  }
}

dependencies {
  compile 'com.github.kevinsawicki:http-request:5.4.1'
  testCompile group: 'junit', name: 'junit', version: '4.+'
}

repositories {
  mavenCentral()
}

jar {
  baseName "sendgrid"
  manifest {
    attributes("Implementation-Title": "MrGadget", "Implementation-Version": version)
  }
}

// adds 'with-dependencies' to the fatJar name
fatJar {
  classifier 'jar-with-dependencies'
  baseName "sendgrid"
  manifest {
    attributes("Implementation-Title": "MrGadget", "Implementation-Version": version)
  }
}

// copy jar and fatJar to base project directory so they will be in git (and on github for download)
build << {
  copy {
    println "Copying ${jar.archiveName} to $projectDir/repo/com_github_scottmotte/sendgrid/$version"
    from("$buildDir/libs/${jar.archiveName}")
    into("$projectDir/repo/com_github_scottmotte/sendgrid/$version")
  }
  copy {
    println "Copying ${fatJar.archiveName} to $projectDir/repo/com_github_scottmotte/sendgrid/$version"
    from("$buildDir/libs/${fatJar.archiveName}")
    into("$projectDir/repo/com_github_scottmotte/sendgrid/$version")
  }
}

// REQUIRED FOR OSS SONATYPE AND MAVEN CENTRAL DEPLOYMENT

apply plugin: 'maven'
apply plugin: 'signing'

group         = "com.github.scottmotte"

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}
 
task sourcesJar(type: Jar, dependsOn:classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives sourcesJar
  archives javadocJar
  archives jar
  archives fatJar
}

signing {
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        //authentication(userName: sonatypeUsername, password: sonatypePassword)
      }

      pom.project {
        name 'Sendgrid'
        packaging 'jar'
        description 'SendGrid Java helper library'
        url 'http://scottmotte.github.io/sendgrid-java/'

        scm {
          url 'scm:git@github.com:scottmotte/sendgrid-java.git'
          connection 'scm:git@github.com:scottmotte/sendgrid-java.git'
          developerConnection 'scm:git@github.com:scottmotte/sendgrid-java.git'
        }

        licenses {
          license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
          }
        }

        developers {
          developer {
            id "scottmotte"
            name "Scott Motte"
          }
        }
      }
    }
  }
}
